plugins {
    id 'base'
    id 'com.github.ben-manes.versions' version '0.15.0'
    id 'com.github.hierynomus.license' version '0.11.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'com.github.hierynomus.license'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        compile "org.slf4j:slf4j-api:$slf4jVersion"

        testCompile "junit:junit:$junitVersion"
        testRuntime("com.h2database:h2:$h2Version") { transitive = false }
        testRuntime "org.slf4j:slf4j-simple:$slf4jVersion"
        testRuntime "org.slf4j:jcl-over-slf4j:$slf4jVersion"
        testRuntime "org.slf4j:jul-to-slf4j:$slf4jVersion"
    }

    license {
        header = rootProject.file('gradle/LICENSE_TEMPLATE')
        strictCheck = true
        ignoreFailures = true
        mapping {
            java = 'SLASHSTAR_STYLE'
            xml  = 'XML_STYLE'
        }
        ext.year    = '2017'
        ext.author  = 'Andres Almiray'
        ext.project = 'JavaTrove Examples'
    }

    test {
        environment = [
            rootDir: rootProject.projectDir.absolutePath
        ]
    }
}
